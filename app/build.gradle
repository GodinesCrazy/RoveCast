plugins {
    id 'com.android.application' version '8.9.0'
}

android {
    namespace 'com.ivanmarty.rovecast'
    compileSdk 35

    defaultConfig {
        applicationId "com.ivanmarty.rovecast"
        minSdk 24
        targetSdk 35
        versionCode 19
        versionName "1.2.9"
        vectorDrawables.useSupportLibrary true
    }

    signingConfigs {
        // Configuraci√≥n de firma mediante propiedades del proyecto o gradle.properties
        // Define en gradle.properties (global o del proyecto):
        // RELEASE_STORE_FILE=/ruta/keystore.jks
        // RELEASE_STORE_PASSWORD=***
        // RELEASE_KEY_ALIAS=***
        // RELEASE_KEY_PASSWORD=***
        release {
            if (project.hasProperty('RELEASE_STORE_FILE')) {
                storeFile file(project.property('RELEASE_STORE_FILE'))
                storePassword project.findProperty('RELEASE_STORE_PASSWORD') ?: ''
                keyAlias project.findProperty('RELEASE_KEY_ALIAS') ?: ''
                keyPassword project.findProperty('RELEASE_KEY_PASSWORD') ?: ''
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (project.hasProperty('RELEASE_STORE_FILE')) {
                signingConfig signingConfigs.release
            }
        }
        debug { minifyEnabled false }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    // Tema (Material Components M2) + AppCompat (NECESARIOS para Theme.MaterialComponents)
    implementation "com.google.android.material:material:1.12.0"
    implementation "androidx.appcompat:appcompat:1.7.1"

    // Media3
    def media3 = "1.8.0"
    implementation "androidx.media3:media3-exoplayer:$media3"
    implementation "androidx.media3:media3-ui:$media3"
    implementation "androidx.media3:media3-session:$media3"

    // Retrofit / OkHttp / Gson
    implementation "com.squareup.retrofit2:retrofit:2.11.0"
    implementation "com.squareup.retrofit2:converter-gson:2.11.0"
    implementation "com.squareup.okhttp3:okhttp:4.12.0"

    // Room
    def room = "2.7.2"
    implementation "androidx.room:room-runtime:$room"
    annotationProcessor "androidx.room:room-compiler:$room"

    // Lifecycle + UI
    implementation "androidx.lifecycle:lifecycle-viewmodel:2.9.1"
    implementation "androidx.lifecycle:lifecycle-livedata:2.9.1"
    implementation "androidx.recyclerview:recyclerview:1.3.2"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.cardview:cardview:1.0.0"

    // Glide
    implementation "com.github.bumptech.glide:glide:4.16.0"
    annotationProcessor "com.github.bumptech.glide:compiler:4.16.0"

    // Shimmer
    implementation 'com.facebook.shimmer:shimmer:0.5.0'

    // Ads + UMP + Review
    implementation "com.google.android.gms:play-services-ads:24.5.0"
    implementation "com.google.android.ump:user-messaging-platform:3.1.0"
    implementation "com.google.android.play:review:2.0.1"

    // Cast
    implementation "com.google.android.gms:play-services-cast-framework:22.1.0"
    implementation "androidx.mediarouter:mediarouter:1.8.1"

    // Billing
    implementation "com.android.billingclient:billing:8.0.0"

    // Pruebas
    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.2.1"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.6.1"
}
